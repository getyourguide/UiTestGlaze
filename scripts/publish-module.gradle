apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                from components.release

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Solid and easy to write Android UI tests'
                    url = 'https://github.com/getyourguide/UiTestGlaze'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/getyourguide/UiTestGlaze/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'BenedictP'
                            name = 'Benedict Pregler'
                            email = 'benedict.pregler@getyourguide.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/getyourguide/UiTestGlaze.git'
                        developerConnection = 'scm:git:ssh://github.com/getyourguide/UiTestGlaze.git'
                        url = 'https://github.com/getyourguide/UiTestGlaze/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
